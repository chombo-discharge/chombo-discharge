C      _______              __
C     / ___/ /  ___  __ _  / /  ___
C    / /__/ _ \/ _ \/  V \/ _ \/ _ \
C    \___/_//_/\___/_/_/_/_.__/\___/
C    Please refer to Copyright.txt, in Chombo's root directory.

#include "CONSTANTS.H"

c ------------------------------------------------------------------
c  this subroutine sets extrapolation BC's in ghost cells
c  INPUTS/OUTPUTS
c  state    <=> what we're doing BC's on
c  bcBox     => ghost cells to fill
c  dir       => direction of face (0->SpaceDim-1)
c  side      => side of face (-1=low, +1 = high)
c  dx        => cell spacing
c  startcomp => first component in state fab to apply BC's to
c  endcomp   => final component to apply BC's to
c -----------------------------------------------------------------
      subroutine EXTRAPGHOSTBC(CHF_FRA[state],
     &                         CHF_BOX[bcBox],
     &                         CHF_CONST_INT[idir],
     &                         CHF_CONST_INT[side],
     &                         CHF_CONST_REAL[dx],
     &                         CHF_CONST_INT[startcomp],
     &                         CHF_CONST_INT[endcomp])


c ::: local variables
      integer nc
      integer CHF_DDECL[ii;jj;kk]
      integer CHF_DDECL[i;j;k]

      REAL_T nearval, farval

      CHF_DTERM[
      ii = side*CHF_ID(0,idir);
      jj = side*CHF_ID(1,idir);
      kk = side*CHF_ID(2,idir) ]

      do nc = startcomp, endcomp

         CHF_MULTIDO[bcBox; i;j;k]
           nearval = state(CHF_IX[i-ii;j-jj;k-kk],nc)
           farval  = state(CHF_IX[i-2*ii;j-2*jj;k-2*kk],nc)

           state(CHF_IX[i;j;k],nc) = two*nearval - farval

         CHF_ENDDO

      enddo

      return
      end



c ------------------------------------------------------------------
c  this subroutine sets higher-order extrapolation BC's in ghost cells
c  INPUTS/OUTPUTS
c  state    <=> what we're doing BC's on
c  bcBox     => ghost cells to fill
c  dir       => direction of face (0->SpaceDim-1)
c  side      => side of face (-1=low, +1 = high)
c  dx        => cell spacing
c  startcomp => first component in state fab to apply BC's to
c  endcomp   => final component to apply BC's to
c -----------------------------------------------------------------
      subroutine HOEXTRAPGHOSTBC(CHF_FRA[state],
     &                         CHF_BOX[bcBox],
     &                         CHF_CONST_INT[idir],
     &                         CHF_CONST_INT[side],
     &                         CHF_CONST_REAL[dx],
     &                         CHF_CONST_INT[startcomp],
     &                         CHF_CONST_INT[endcomp])


c ::: local variables
      integer nc
      integer CHF_DDECL[ii;jj;kk]
      integer CHF_DDECL[i;j;k]

      REAL_T nearval, midval, farval

      CHF_DTERM[
      ii = side*CHF_ID(0,idir);
      jj = side*CHF_ID(1,idir);
      kk = side*CHF_ID(2,idir) ]

      do nc = startcomp, endcomp

         CHF_MULTIDO[bcBox; i;j;k]
           nearval = state(CHF_IX[i-ii;j-jj;k-kk],nc)
           midval  = state(CHF_IX[i-2*ii;j-2*jj;k-2*kk],nc)
           farval  = state(CHF_IX[i-3*ii;j-3*jj;k-3*kk],nc)

           state(CHF_IX[i;j;k],nc) = three*(nearval - midval) + farval

         CHF_ENDDO

      enddo

      return
      end


c ------------------------------------------------------------------
c  this subroutine sets reflection (even) BC's in ghost cells
c  (note that reflect-even is same as 0th order extrap)
c  INPUTS/OUTPUTS
c  state    <=> what we're doing BC's on
c  bcBox     => ghost cells to fill
c  dir       => direction of face (0->SpaceDim-1)
c  side      => side of face (-1=low, +1 = high)
c  dx        => cell spacing
c  startcomp => first component in state fab to apply BC's to
c  endcomp   => final component to apply BC's to
c  scale     => +1 = reflect-even,-1=reflect-odd, etc
c -----------------------------------------------------------------
      subroutine REFLECTGHOSTBC(CHF_FRA[state],
     &                          CHF_BOX[bcBox],
     &                          CHF_CONST_INT[idir],
     &                          CHF_CONST_INT[side],
     &                          CHF_CONST_REAL[dx],
     &                          CHF_CONST_INT[startcomp],
     &                          CHF_CONST_INT[endcomp],
     &                          CHF_CONST_REAL[scale])


c ::: local variables
      integer CHF_DDECL[ii;jj;kk]
      integer CHF_DDECL[i;j;k]
      integer n

      if((side .ne. -1).and.(side.ne.1)) then
         call MAYDAYERROR()
      endif

      do n = startcomp, endcomp
         CHF_MULTIDO[bcBox; i; j; k]

c ::: this is the case of a low-side boundary
         if (side.eq.-1)  then
           CHF_DTERM[
            ii = CHF_ID(0,idir)*(2*(CHF_UBOUND[bcBox;0]-i)+1);
            jj = CHF_ID(1,idir)*(2*(CHF_UBOUND[bcBox;1]-j)+1);
            kk = CHF_ID(2,idir)*(2*(CHF_UBOUND[bcBox;2]-k)+1) ]
         else if (side.eq.1) then
c ::: hi-side boundary
           CHF_DTERM[
           ii = CHF_ID(0,idir)*(2*(CHF_LBOUND[bcBox;0]-i)-1);
           jj = CHF_ID(1,idir)*(2*(CHF_LBOUND[bcBox;1]-j)-1);
           kk = CHF_ID(2,idir)*(2*(CHF_LBOUND[bcBox;2]-k)-1) ]
        else
c  ::: should never get here
           call MAYDAYERROR()
        endif

c ::: now copy (and rescale) valid-region value
        state(CHF_IX[i;j;k],n) = scale*state(CHF_IX[i+ii; j+jj; k+kk],n)

        CHF_ENDDO
      enddo

      return
      end

